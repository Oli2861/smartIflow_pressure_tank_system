package control_circuit;

import definitions.*;

class PressureSwitch {
    Ports:
        // Pressure sensor (reads tank pressure) input controlling whether the coil is energized.
        PressureInfo pressureInfo;
        // Client connections to be connected.
        Electrical clientPowerConnection1;
        Electrical clientPowerConnection2;

    Variables:
        // If the pressure switch is defect, it does not detect excess pressure.
        Enum[Nominal, Defect] failureMode = Nominal;
        // Initially not pressurized
        Boolean isPressurized = False;

    Events:
        Defect { type = failure, p = 0.1e-9};

    Behavior:
        // Connected, if the tank is not pressurized.
        if(isPressurized == False) {
            connect { r = 1 } (clientPowerConnection1, clientPowerConnection2);
        }

    Transitions:
        when(pressureInfo.exists(pressurized = True) && failureMode == Nominal) {
            isPressurized = True;
        }
        when(pressureInfo.exists(pressurized = False)) {
            isPressurized = False;
        }

    EventHandlers:
        when(Defect) {
            failureMode = Defect;
        }

}
