package valve_system;

import definitions.*;

class PressureTank {
    Ports:
        // Fluid received from pump.
        Fluid fluidInput;
        // Pressure output to the pressure switch.
        PressureInfo pressureInfo;


    Variables:
        Enum[Unpressured, Pressurized, Damaged] status = Unpressured;
        Boolean isReceivingPressure = False;

    Events:
        TankFailure { type = failure };

    Behavior:
        // Update pressure sensor information
        if(status == Pressurized) {
            set(pressureInfo, { pressurized = True });
        }
        if(status == Unpressured) {
            set(pressureInfo, { pressurized = False });
        }

    Transitions:
        // Update whether the tank is currently filled by the pump
        when (fluidInput.exists(running = True)) {
            isReceivingPressure = True;
        }
        when (fluidInput.exists(running = False)) {
            isReceivingPressure = False;
        }

        // As water is pumped into the pressure tank, the tank becomes pressurized.
        when { delay = 2 } (fluidInput.exists(running = True))
        [fluidInput.exists(running = True)] {
            status = Pressurized;
        }
        // If pressured still after 4 OM of time --> Damaged.
        when { delay = 4 } (fluidInput.exists(running = True))
        [fluidInput.exists(running = True)] {
            status = Damaged;
        }

    EventHandlers:
        when(TankFailure) {
            status = Damaged;
        }

}
